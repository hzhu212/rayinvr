c
c     version 1.2  Mar 1992
c                 
c     ----------------------------------------------------------------
c     |                                                              |
c     |            *********  R A Y P L O T  **********              |
c     |                                                              |
c     |               Produce plot using the 'p.out'                 |
c     |                       file as input                          |
c     |                                                              |
c     |                   Written by C. A. Zelt                      |
c     |                                                              |
c     |                Geological Survey of Canada                   |   
c     |                  Ottawa, Canada K1A 0Y3                      |
c     |                                                              |
c     ----------------------------------------------------------------
c                 
c                 
c     I/O units:  
c                 
c        19 -- input:  plot commands generated by RAYINVR
c              
c        21 -- input:  origin coordinates for single-panel plot
c
c
c     ----------------------------------------------------------------
c                 
c 
      program main
c
c
      parameter(nmax=10000)
      common /cplot/ iplot,isep,iseg,nseg,xwndow,ywndow,ibcol,ifcol,sf
c
      real xa(nmax),ya(nmax)
      character label(80),reply*1,title*40
c
      sf=1.2
      i33=0
      isep=0
      nseg=0 
      xwndow=0.
      ywndow=0.
      ipan=0
c
110   write(6,105)
105   format(/'Enter iroute, iseg and iplot')
      read(5,*) iroute,iseg,iplot
      if(iroute.lt.1.or.iroute.gt.5.or.iseg.lt.0.or.iseg.gt.1.
     +   or.iplot.lt.0.or.iplot.gt.2) 
     +  go to 110
c
      if(iplot.eq.0) iplot=-1
      if(iplot.eq.2) iplot=0
c
      if(iroute.eq.1) then
        write(6,115)
115     format('Do you want to specify the window size? (y or n)')
        read(5,125) reply
125     format(a1)
        if(reply(1:1).eq.'y'.or.reply(1:1).eq.'Y') then
          write(6,135)
135       format('Enter xwndow and ywndow (mm)')
          read(5,*) xwndow,ywndow
        else
          xwndow=290.
          ywndow=180.
        end if
      end if
      write(6,145)
145   format('Enter x and y scale factors')
      read(5,*) xsf,ysf
c      asf=min(xsf,ysf)
      asf=(xsf+ysf)/2.
      write(6,155)
155   format('Do you want to form plot panels? (y or n)')
      read(5,125) reply
      if(reply(1:1).eq.'y'.or.reply(1:1).eq.'Y') then
        open(unit=21, file='p.in', status='old')
        ipan=1
        read(21,*,end =990) x0,y0,xt,yt,tht,title
      end if
      write(6,165)
165   format(
     +  'Do you want to select minimum-traveltime raypaths? (y or n)')
      read(5,125) reply
      if(reply(1:1).eq.'y'.or.reply(1:1).eq.'Y') then
        open(unit=33, file='2pt.out', status='old')
        i33=1
      end if
c
      open(unit=20, file='p.out', status='old')
      if(iplot.le.0) 
     +  open(unit=19, file='post.out')
c
100   read(20,5,end=999) itype
5     format(i2)
c
      go to (1001,2001,9999,1000,2000,3000,4000,5000,6000,7000,
     +       8000,9000,10000), itype+3
      go to 9999
c
2001  read(20,35) ibcol,ifcol
      call plots(xwndow,ywndow,iroute)
      call segmnt(1)
      go to 100
c
1001  if(ipan.eq.0) call erase
      go to 100
c
1000  read(20,15) x,y,ipen
15    format(2e15.5,i10)
      x=x*xsf+x0
      y=y*ysf+y0
      call plot(x,y,ipen)
      go to 100
c
2000  read(20,25) x,y,albht,value,angle,ndeci
25    format(5e15.5,i10)
      x=x*xsf+x0
      y=y*ysf+y0
      albht=albht*asf
      call number(x,y,albht,value,angle,ndeci)
      go to 100
c
3000  read(20,35) nchar
35    format(2i10)
      read(20,45) x,y,albht,angle,(label(i),i=1,nchar)
45    format(4e15.5,<nchar>a1)
      x=x*xsf+x0
      y=y*ysf+y0
      albht=albht*asf
      call symbol(x,y,albht,label,angle,nchar)
      go to 100
c
4000  call empty
      go to 100
c
5000  if(ipan.eq.0) then
        call aldone
      else
        call symbol(x0+xt,y0+yt,tht,title,0.,40)
        read(21,*,end=990) x0,y0,xt,yt,tht,title
      end if
      go to 100
c
6000  if(ipan.eq.1) call symbol(x0+xt,y0+yt,tht,title,0.,40)
      call plotnd(1)
      go to 100
c
7000  read(20,35) icol
      call pcolor(icol)
      go to 100
c
8000  read(20,35) iflag
      if(ipan.eq.0) call segmnt(iflag)
      go to 100
c
9000  read(20,35) npts
      read(20,55) (xa(i),ya(i),i=1,npts)
55    format(2e15.5)
c
      if(i33.eq.1) then
        read(33,335) il
335     format(i1)
        if(il.eq.0) go to 100
      end if
c
      do 910 i=1,npts
         xa(i)=xa(i)*xsf+x0
         ya(i)=ya(i)*ysf+y0
910   continue
      call line(xa,ya,npts)
      go to 100
c
10000 read(20,65) x,y,size,icol
65    format(3e15.5,i10)
      x=x*xsf+x0
      y=y*ysf+y0
      call dot(x,y,size,icol)
      go to 100
c
990   write(6,905)
905   format(/'***  end-of-file reached in p.in  ***'/)
      stop
c
9999  write(6,995)
995   format(/'***  error in format of p.out file  ***'/)
c
999   stop
      end
